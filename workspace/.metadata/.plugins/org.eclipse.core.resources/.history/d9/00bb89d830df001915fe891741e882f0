scchart pacemaker {
  input signal ASense, VSense;
  output signal AEIStart, AVIStart, PVARPStart, VRPStart, URIStart, LRIStart;
  input signal AEIExpired, AVIExpired, PVARPExpired, VRPExpired, URIExpired, LRIExpired;
  output signal AEIStop, AVIStop, PVARPStop, VRPStop, URIStop, LRIStop;
  output signal APace, VPace;

  initial state main { 
  	/* AVI – Atrioventricular Interval
	The maximum time between an atrial event and its subsequent ventricular event */
    region AVI:
	
	initial state idle
		--> PVARPWait with ASense
		--> V_wait with APace; 
	state PVARPWait
		--> ASWait with PVARPExpired;
	state ASWait 
		--> V_wait with ASense
		--> V_wait with APace;
	connector state V_wait
		--> AVI_on immediate with / AVIStart;
	state AVI_on
		--> idle with VSense / AVIStop
		--> idle with AVIExpired;
	
	/* AVITimer – Atrioventricular Interval Timer
	The timer state machine that handles pacing based on the AVI timer */
    region AVITimer:
	
	initial state idle
		--> URIDone with URIExpired
		--> AVITimer with AVIStart;
	state URIDone //URI expiry pulse has already been detected, we are clear to pace whenever AVI is activated
		--> AVITimerURIDone with AVIStart;
	state AVITimer
		--> idle with AVIStop
		--> URIWait with AVIExpired / VPace;
	state URIWait
		--> idle with URIExpired / VPace;
	state AVITimerURIDone
		--> idle with AVIStop
		--> idle with AVIExpired / VPace;
	
	/* PVARP – Post-Ventricular Atrial Refractory Period
	The time after a ventricular event where any atrial events are ignored as Atrial
	Refractory (AR) signals */
    region PVARP:
    
   	initial state idle
    	--> AR_wait with VSense
		--> AR_wait with VPace;
	connector state AR_wait
		--> PVARPWait with / PVARPStart;
	state PVARPWait
		--> idle with PVARPExpired;
		
	/* VRP – Ventricular Refractory Period
	The time after a ventricular event where any other ventricular events are ignored as
	Ventricular Refractory (VR) signals */
    region VRP:

    initial state idle
    	--> V_wait with VSense
		--> V_wait with VPace;
	connector state V_wait
		--> VRPWait with / VRPStart;
	state VRPWait
		--> idle with VRPExpired
		--> VSenseCheck with VSense;
	connector state VSenseCheck
		--> VRPWait;
    
	/* AEI – Atrial Escape Interval
	The maximum time between a ventricular event and its subsequent atrial event */
    region AEI:

    initial state idle
		--> VRPWait with VSense
		--> A_wait with VPace;
	state VRPWait
		--> VSWait with VRPExpired;
	state VSWait
		--> A_wait with VSense
		--> A_wait with VPace;
	connector state A_wait
		--> AEI_on immediate with / AEIStart;
	state AEI_on
		--> idle with ASense / AEIStop
		--> idle with AEIExpired;

	/* AEITimer – Atrial Escape Interval Timer
	The timer state machine that handles pacing based on the AVI timer */
    region AEITimer:
	
	initial state idle
		--> AEITimer with AEIStart;
	state AEITimer
		--> idle with AEIStop
		--> idle with AEIExpired / APace;

	/* LRI – Lower Rate Interval
	The slowest rate at which the heart is allowed to operate. This is measured as the time
	between ventricular events */
    region LRI:
    
    initial state idle
		--> VRPWait with VSense / LRIStart;
	state VRPWait
		--> LRI_on with VRPExpired;
	state LRI_on
		--> resetLRI immediate with VSense / LRIStop
		--> resetLRI immediate with VPace / LRIStop
		--> resetLRI immediate with LRIExpired / VPace;
	connector state resetLRI
		--> VRPWait immediate with / LRIStart;
    
    /* LRITimer – Lower Rate Interval Timer
	This is the timer that handles pacing based on the LRI Timer */
    region LRITimer:

    initial state idle
		--> LRITimer with LRIStart;
	state LRITimer
		--> idle with LRIExpired
		--> idle with LRIStop;
		
	/* URI – Upper Rate Interval
	The fastest rate at which the pacemaker will ever pace the heart at. This is measured
	as the time between ventricular events */
    region URI:

    initial state idle
		--> VRPWait with VSense
		--> URI_wait with VPace;
	state VRPWait 
		--> VSWait with VRPExpired;
	state VSWait
		--> URI_wait with VSense
		--> URI_wait with VPace;
	connector state URI_wait
		--> URI_on immediate with / URIStart;
	state URI_on
		--> idle with URIExpired;
	
	/* URITimer – Upper Rate Interval Timer
	This is the timer that handles pacing based on the URI Timer */
    region URITimer:

    initial state idle
		--> URITimer with URIStart;
	state URITimer
		--> idle with URIExpired
		--> idle with URIStop;
	
  };
}